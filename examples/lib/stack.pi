#attach: bool.pi
#global: stack

r<<stack; +push,pop,_pop,peek;(
  ! Empty item
  +bottom;(
    <>bottom; push,pop,peek->r.
    ack<<bottom;->ack; cascade,ret<<-_pop; +if,else,finish;(
      if,else>->cascade.
      <-if; finish->bottom.
      <-else; ->finish.
      <-finish; +none; tt,none,bottom->ret.
    )
  )

  ! Push item
  x,ack<<<push; +ret; ff,ret>->_pop; _,_,prev<-ret; +create;(
    ack->create.
    ack<<create;->ack; cascade,ret<<-_pop; +if,else,finish;(
      if,else>->cascade.
      <-if; finish->prev.
      <-else; ->finish.
      <-finish; ff,x,create->ret.
    )
  )

  ! Pop item
  c<<pop; +ret; tt,ret>->_pop; empty,x<-ret; empty,x->c.

  ! Peek at top item
  c<<peek; +ret; ff,ret>->_pop; _,x,create<-ret; <>create; x->c.
)